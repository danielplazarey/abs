# $Id: PKGBUILD 87070 2013-03-26 17:39:40Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-gstreamer0.10
pkgver=0.10.36
pkgrel=3
pkgdesc="GStreamer Multimedia Framework"
arch=('i386' 'amd64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=("${pkgname#*-}" 'lib32-glib2' 'lib32-libxml2')
makedepends=('lib32-devel' 'intltool')
options=('!libtool' 'libtoolfix')
source=("http://gstreamer.freedesktop.org/src/gstreamer/gstreamer-${pkgver}.tar.xz"
	patch-Makefile.in
	patch-gst_gstdatetime.c
	patch-support-bison3)
sha256sums=('9151aa108c177054387885763fa0e433e76780f7c5655c70a5390f2a6c6871da'
            'bf34fe71622c6ae3688d8c124689edbb9ec88a1d7eea1b84ccc9132be97afbc6'
            'ee5f60891a1a75dd49c26225e8b6eb729722c4a59c0ac9d7f51f218b7ae76279'
            'e21832997787459c43004d6f5e03ce507beb6a1b57698bb91969f98f0ad6acb3')

build() {
  cd "${srcdir}"/gstreamer-${pkgver}

   for i in patch-Makefile.in \
		patch-gst_gstdatetime.c patch-support-bison3; do
   patch -p0 -i "${srcdir}/$i"
   done

# Multilib
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

# Build
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libdir=/usr/lib32 --libexecdir=/usr/lib32 --with-package-{name="GStreamer (ArchBSD Multilib)",origin="http://www.archbsd.net"} --disable-{examples,static,tests} \
	--disable-failing-tests --disable-gtk-doc

  gmake
}

package() {
  cd "${srcdir}"/gstreamer-${pkgver}

# Install
  gmake DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}

# vim: ts=2 sw=2 et:
