# $Id: PKGBUILD 87149 2013-03-28 07:09:50Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Nicky726 <nicky726@gmail.com>
# Contributor: Tom Killian <tom@archlinux.org>
# Contributor: RÃ©my Oudompheng <remyoudompheng@gmail.com>

pkgname=lib32-libtheora
pkgver=1.1.1
pkgrel=10
pkgdesc="An open video codec developed by the Xiph.org"
arch=('i386' 'amd64')
url="http://www.xiph.org"
license=('BSD')
depends=('lib32-libogg' "${pkgname#*-}")
makedepends=('lib32-devel' 'lib32-libvorbis')
options=('!pathfix' 'pathfix32')
source=("http://downloads.xiph.org/releases/theora/${pkgname#*-}-${pkgver}.tar.bz2"
	libtheora-1.1.1-libpng16.patch)
sha256sums=('b6ae1ee2fa3d42ac489287d3ec34c5885730b1296f0801ae577a35193d3affbc'
            'e4c9a8dc798c596ed32a2a720020ae27a0e72f5add1a47cb8fadebe0e7180d7e')

build() {
  cd "${srcdir}"/${pkgname#*-}-${pkgver}

# Patch
  gsed -i 's|AC_DEFINE(\[OC_X86_64_ASM\]|#AC_DEFINE(\[OC_X86_64_ASM\]|
          s|AM_CONFIG_HEADER|AC_CONFIG_HEADERS|' configure.ac

  autoconf

	sed -i '' -e 's/-fforce-addr//g' configure

	patch -Np0 -i ../libtheora-1.1.1-libpng16.patch

# Multilib
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"

# Build
  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-{examples,static} --with-pic
  gmake
}

package() {
  cd "${srcdir}"/${pkgname#*-}-${pkgver}

# Install
  gmake -j1 DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}

# License
  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s ${pkgname#*-} "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
