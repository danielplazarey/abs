# $Id: PKGBUILD 97335 2013-09-18 07:46:23Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=lib32-orc
pkgver=0.4.19
pkgrel=1
pkgdesc="The Oild Runtime Compiler"
arch=('i386' 'amd64')
license=('custom')
url="http://code.entropywave.com/projects/orc/"
depends=('lib32-libc' "${pkgname#*-}")
makedepends=('valgrind' 'lib32-devel')
options=('!libtool')
source=("http://gstreamer.freedesktop.org/src/orc/${pkgname#*-}-${pkgver}.tar.gz")
sha256sums=('e21f50321a21c33178bbe41ea8bd27c49ae60aeb8c22902a215f7141297fb59e')

build() {
  cd ${pkgname#*-}-${pkgver}

  export CC='clang -m32'
  export CXX='clang++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  sed -i '' -E \
    -e '/LIBS/s/-lrt(\ )*//; /LIBRT/s/-lrt//' \
    configure

  ./configure --prefix='/usr' --libdir='/usr/lib32' --disable-static
  gmake SUBDIRS="orc orc-test tools"
}

package() {
  cd ${pkgname#*-}-${pkgver}

  gmake SUBDIRS="orc orc-test tools" DESTDIR="$pkgdir" install
#	cp -R "${pkgdir}"/usr/lib "${pkgdir}"/usr/lib32
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s ${pkgname#*-} "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
