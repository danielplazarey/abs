# $Id: PKGBUILD 87075 2013-03-26 17:45:48Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-soundtouch
pkgver=1.9.0
pkgrel=1
pkgdesc="An audio processing library"
arch=('i386' 'amd64')
url="http://www.surina.net/soundtouch/"
license=('LGPL')
depends=('lib32-gcc-libs' "${pkgname#*-}")
makedepends=('lib32-devel')
options=('!libtool')
source=("http://www.surina.net/soundtouch/${pkgname#*-}-${pkgver}.tar.gz")
sha256sums=('1fe4fc03cbfe696c1281fd623d089baffe1b70265acc60eac7a6d9b5742971ad')

build() {
  cd "${srcdir}"/${pkgname#*-}

# Patch
  gsed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADER/' configure.ac
  find . -type f | xargs sed -i '' -e 's/-fcheck-new//g'
# Multilib
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

# Build
  ./bootstrap
  ./configure --prefix=/usr --libdir=/usr/lib32 --enable-shared --with-pic
  gmake
}

package() {
  cd "${srcdir}"/${pkgname#*-}

# Install
  gmake DESTDIR="${pkgdir}" install

# Multilib
  rm -rf "${pkgdir}"/usr/{bin,doc,include,share}
}

# vim: ts=2 sw=2 et:
