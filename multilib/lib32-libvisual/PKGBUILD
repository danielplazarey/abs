# $Id: PKGBUILD 89187 2013-04-27 17:54:48Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-libvisual
pkgver=0.4.0
pkgrel=5
pkgdesc="Abstraction library that comes between applications and audio visualisation plugins"
arch=('i386' 'amd64')
url="http://sourceforge.net/projects/libvisual/"
license=('LGPL')
depends=('lib32-libc' "${pkgname#*-}")
makedepends=('lib32-devel')
options=('!libtool' 'libtoolfix')
patchdir=("${pkgname#*-*}-${pkgver}")
source=("http://downloads.sourceforge.net/sourceforge/${pkgname#*-}/${pkgname#*-}-${pkgver}.tar.gz")
sha256sums=('0b4dfdb87125e129567752089e3c8b54cefed601eef169d2533d8659da8dc1d7')

build() {
	cd "${srcdir}"/${pkgname#*-*}-${pkgver}

	sed -i '' -e 's|-Wno-unused-variable||g' -e 's|-O3|${CFLAGS}|g' libvisual/Makefile.in

	export CC="clang -m32"
	export CXX="clang++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	./configure --prefix=/usr --libdir=/usr/lib32
	gmake
}

package() {
	cd "${srcdir}"/${pkgname#*-*}-${pkgver}

	gmake DESTDIR="${pkgdir}" install
	rm -rf "${pkgdir}"/usr/{include,share}
}

# vim: ts=2 sw=2 et:
